//Type to print
type="both"; //[bottom,top,both]

//Card size long edge
CARD_SIZE_LONG_EDGE =91;

//Card size short edge
CARD_SIZE_SHORT_EDGE = 63;

//Card stack total height
CARD_STACK_HEIGHT = 18;

//Wall thickness of the box, e.g. 2mm
WALL_THICKNESS = 2;

//Width of the tab which keeps the two halfes together
TAB_THICKNESS = 10;

//wedge size at end of tab. Has to be less than "wall_thickness" above!
Wedge_thickness=1.6;
    
//wiggle space so the boxes slide easily on top of each other.
EXTRA_SPACE = .25;

//Is there a hole in the top?
Cutout_hole_top = "true"; //[true, false]

//Is there a hole in the bottom?
Cutout_hole_bottom = "false"; //[true, false]

//Angle of the cutout (looks nice)
Cutout_angle=30;

//Set the text to render on the top lid
Top_Text = "Uno";
 
//Bottom text
Bottom_Text = "Junior";

//Set text size in mm
textsize=9; 

//Font name, e.g. "Libration Sans", "Aldo", depends on what is available on the platform. In the customizer,choose from about 700 Google Fonts. For an overview please refer: http://www.google.com/fonts
fontname="Orbitron"; //[ABeeZee,Abel,Abril Fatface,Aclonica,Acme,Actor,Adamina,Advent Pro,Aguafina Script,Akronim,Aladin,Aldrich,Alef,Alegreya,Alegreya Sans,Alegreya Sans SC,Alegreya SC,Alex Brush,Alfa Slab One,Alice,Alike,Alike Angular,Allan,Allerta,Allerta Stencil,Allura,Almendra,Almendra Display,Almendra SC,Amarante,Amaranth,Amatic SC,Amethysta,Amiri,Anaheim,Andada,Andika,Angkor,Annie Use Your Telescope,Anonymous Pro,Antic,Antic Didone,Antic Slab,Anton,Arapey,Arbutus,Arbutus Slab,Architects Daughter,Archivo Black,Archivo Narrow,Arimo,Arizonia,Armata,Artifika,Arvo,Asap,Asset,Astloch,Asul,Atomic Age,Aubrey,Audiowide,Autour One,Average,Average Sans,Averia Gruesa Libre,Averia Libre,Averia Sans Libre,Averia Serif Libre,Bad Script,Balthazar,Bangers,Basic,Battambang,Baumans,Bayon,Belgrano,Belleza,BenchNine,Bentham,Berkshire Swash,Bevan,Bigelow Rules,Bigshot One,Bilbo,Bilbo Swash Caps,Bitter,Black Ops One,Bokor,Bonbon,Boogaloo,Bowlby One,Bowlby One SC,Brawler,Bree Serif,Bubblegum Sans,Bubbler One,Buda,Buenard,Butcherman,Butterfly Kids,Cabin,Cabin Condensed,Cabin Sketch,Caesar Dressing,Cagliostro,Calligraffitti,Cambay,Cambo,Candal,Cantarell,Cantata One,Cantora One,Capriola,Cardo,Carme,Carrois Gothic,Carrois Gothic SC,Carter One,Caudex,Cedarville Cursive,Ceviche One,Changa One,Chango,Chau Philomene One,Chela One,Chelsea Market,Chenla,Cherry Cream Soda,Cherry Swash,Chewy,Chicle,Chivo,Cinzel,Cinzel Decorative,Clicker Script,Coda,Coda Caption,Codystar,Combo,Comfortaa,Coming Soon,Concert One,Condiment,Content,Contrail One,Convergence,Cookie,Copse,Corben,Courgette,Cousine,Coustard,Covered By Your Grace,Crafty Girls,Creepster,Crete Round,Crimson Text,Croissant One,Crushed,Cuprum,Cutive,Cutive Mono,Damion,Dancing Script,Dangrek,Dawning of a New Day,Days One,Dekko,Delius,Delius Swash Caps,Delius Unicase,Della Respira,Denk One,Devonshire,Dhurjati,Didact Gothic,Diplomata,Diplomata SC,Domine,Donegal One,Doppio One,Dorsa,Dosis,Dr Sugiyama,Droid Sans,Droid Sans Mono,Droid Serif,Duru Sans,Dynalight,Eagle Lake,Eater,EB Garamond,Economica,Ek Mukta,Electrolize,Elsie,Elsie Swash Caps,Emblema One,Emilys Candy,Engagement,Englebert,Enriqueta,Erica One,Esteban,Euphoria Script,Ewert,Exo,Exo 2,Expletus Sans,Fanwood Text,Fascinate,Fascinate Inline,Faster One,Fasthand,Fauna One,Federant,Federo,Felipa,Fenix,Finger Paint,Fira Mono,Fira Sans,Fjalla One,Fjord One,Flamenco,Flavors,Fondamento,Fontdiner Swanky,Forum,Francois One,Freckle Face,Fredericka the Great,Fredoka One,Freehand,Fresca,Frijole,Fruktur,Fugaz One,Gabriela,Gafata,Galdeano,Galindo,Gentium Basic,Gentium Book Basic,Geo,Geostar,Geostar Fill,Germania One,GFS Didot,GFS Neohellenic,Gidugu,Gilda Display,Give You Glory,Glass Antiqua,Glegoo,Gloria Hallelujah,Goblin One,Gochi Hand,Gorditas,Goudy Bookletter 1911,Graduate,Grand Hotel,Gravitas One,Great Vibes,Griffy,Gruppo,Gudea,Gurajada,Habibi,Halant,Hammersmith One,Hanalei,Hanalei Fill,Handlee,Hanuman,Happy Monkey,Headland One,Henny Penny,Herr Von Muellerhoff,Hind,Holtwood One SC,Homemade Apple,Homenaje,Iceberg,Iceland,IM Fell Double Pica,IM Fell Double Pica SC,IM Fell DW Pica,IM Fell DW Pica SC,IM Fell English,IM Fell English SC,IM Fell French Canon,IM Fell French Canon SC,IM Fell Great Primer,IM Fell Great Primer SC,Imprima,Inconsolata,Inder,Indie Flower,Inika,Irish Grover,Istok Web,Italiana,Italianno,Jacques Francois,Jacques Francois Shadow,Jim Nightshade,Jockey One,Jolly Lodger,Josefin Sans,Josefin Slab,Joti One,Judson,Julee,Julius Sans One,Junge,Jura,Just Another Hand,Just Me Again Down Here,Kalam,Kameron,Kantumruy,Karla,Karma,Kaushan Script,Kavoon,Kdam Thmor,Keania One,Kelly Slab,Kenia,Khand,Khmer,Khula,Kite One,Knewave,Kotta One,Koulen,Kranky,Kreon,Kristi,Krona One,La Belle Aurore,Laila,Lakki Reddy,Lancelot,Lateef,Lato,League Script,Leckerli One,Ledger,Lekton,Lemon,Liberation Sans,Libre Baskerville,Life Savers,Lilita One,Lily Script One,Limelight,Linden Hill,Lobster,Lobster Two,Londrina Outline,Londrina Shadow,Londrina Sketch,Londrina Solid,Lora,Love Ya Like A Sister,Loved by the King,Lovers Quarrel,Luckiest Guy,Lusitana,Lustria,Macondo,Macondo Swash Caps,Magra,Maiden Orange,Mako,Mallanna,Mandali,Marcellus,Marcellus SC,Marck Script,Margarine,Marko One,Marmelad,Martel Sans,Marvel,Mate,Mate SC,Maven Pro,McLaren,Meddon,MedievalSharp,Medula One,Megrim,Meie Script,Merienda,Merienda One,Merriweather,Merriweather Sans,Metal,Metal Mania,Metamorphous,Metrophobic,Michroma,Milonga,Miltonian,Miltonian Tattoo,Miniver,Miss Fajardose,Modak,Modern Antiqua,Molengo,Molle,Monda,Monofett,Monoton,Monsieur La Doulaise,Montaga,Montez,Montserrat,Montserrat Alternates,Montserrat Subrayada,Moul,Moulpali,Mountains of Christmas,Mouse Memoirs,Mr Bedfort,Mr Dafoe,Mr De Haviland,Mrs Saint Delafield,Mrs Sheppards,Muli,Mystery Quest,Neucha,Neuton,New Rocker,News Cycle,Niconne,Nixie One,Nobile,Nokora,Norican,Nosifer,Nothing You Could Do,Noticia Text,Noto Sans,Noto Serif,Nova Cut,Nova Flat,Nova Mono,Nova Oval,Nova Round,Nova Script,Nova Slim,Nova Square,NTR,Numans,Nunito,Odor Mean Chey,Offside,Old Standard TT,Oldenburg,Oleo Script,Oleo Script Swash Caps,Open Sans,Open Sans Condensed,Oranienbaum,Orbitron,Oregano,Orienta,Original Surfer,Oswald,Over the Rainbow,Overlock,Overlock SC,Ovo,Oxygen,Oxygen Mono,Pacifico,Paprika,Parisienne,Passero One,Passion One,Pathway Gothic One,Patrick Hand,Patrick Hand SC,Patua One,Paytone One,Peddana,Peralta,Permanent Marker,Petit Formal Script,Petrona,Philosopher,Piedra,Pinyon Script,Pirata One,Plaster,Play,Playball,Playfair Display,Playfair Display SC,Podkova,Poiret One,Poller One,Poly,Pompiere,Pontano Sans,Port Lligat Sans,Port Lligat Slab,Prata,Preahvihear,Press Start 2P,Princess Sofia,Prociono,Prosto One,PT Mono,PT Sans,PT Sans Caption,PT Sans Narrow,PT Serif,PT Serif Caption,Puritan,Purple Purse,Quando,Quantico,Quattrocento,Quattrocento Sans,Questrial,Quicksand,Quintessential,Qwigley,Racing Sans One,Radley,Rajdhani,Raleway,Raleway Dots,Ramabhadra,Ramaraja,Rambla,Rammetto One,Ranchers,Rancho,Ranga,Rationale,Ravi Prakash,Redressed,Reenie Beanie,Revalia,Ribeye,Ribeye Marrow,Righteous,Risque,Roboto,Roboto Condensed,Roboto Slab,Rochester,Rock Salt,Rokkitt,Romanesco,Ropa Sans,Rosario,Rosarivo,Rouge Script,Rozha One,Rubik Mono One,Rubik One,Ruda,Rufina,Ruge Boogie,Ruluko,Rum Raisin,Ruslan Display,Russo One,Ruthie,Rye,Sacramento,Sail,Salsa,Sanchez,Sancreek,Sansita One,Sarina,Sarpanch,Satisfy,Scada,Scheherazade,Schoolbell,Seaweed Script,Sevillana,Seymour One,Shadows Into Light,Shadows Into Light Two,Shanti,Share,Share Tech,Share Tech Mono,Shojumaru,Short Stack,Siemreap,Sigmar One,Signika,Signika Negative,Simonetta,Sintony,Sirin Stencil,Six Caps,Skranji,Slabo 13px,Slabo 27px,Slackey,Smokum,Smythe,Sniglet,Snippet,Snowburst One,Sofadi One,Sofia,Sonsie One,Sorts Mill Goudy,Source Code Pro,Source Sans Pro,Source Serif Pro,Special Elite,Spicy Rice,Spinnaker,Spirax,Squada One,Sree Krushnadevaraya,Stalemate,Stalinist One,Stardos Stencil,Stint Ultra Condensed,Stint Ultra Expanded,Stoke,Strait,Sue Ellen Francisco,Sunshiney,Supermercado One,Suranna,Suravaram,Suwannaphum,Swanky and Moo Moo,Syncopate,Tangerine,Taprom,Tauri,Teko,Telex,Tenali Ramakrishna,Tenor Sans,Text Me One,The Girl Next Door,Tienne,Timmana,Tinos,Titan One,Titillium Web,Trade Winds,Trocchi,Trochut,Trykker,Tulpen One,Ubuntu,Ubuntu Condensed,Ubuntu Mono,Ultra,Uncial Antiqua,Underdog,Unica One,UnifrakturCook,UnifrakturMaguntia,Unkempt,Unlock,Unna,Vampiro One,Varela,Varela Round,Vast Shadow,Vesper Libre,Vibur,Vidaloka,Viga,Voces,Volkhov,Vollkorn,Voltaire,VT323,Waiting for the Sunrise,Wallpoet,Walter Turncoat,Warnes,Wellfleet,Wendy One,Wire One,Yanone Kaffeesatz,Yellowtail,Yeseva One,Yesteryear,Zeyada]

// rounded cutouts remove a "circle" from the bottom surface, and a 1/2 "circle" from each short end of the bottom piece

/* [Cutouts] */
// Percent of card deck height to cutout
END_CUTOUT_HEIGHT_PERCENTAGE = 95; // [0:100]

// Percent of box width to cutout
END_CUTOUT_WIDTH_PERCENTAGE  = 67; // [0:100]

// Percent of box width to cutout
BOTTOM_CUTOUT_WIDTH_PERCENTAGE = 67; // [0:100]

// percent of box length to cutout
BOTTOM_CUTOUT_LENGTH_PERCENTAGE = 67; // [0:100]
// preview[view:south west, tilt:top diagonal]

/* [Hidden] */
//-------------------------------------------------------------------------------

/*
Customizable card box

Remixed from:

https://www.thingiverse.com/thing:137961
"Customizable Card Box with Cutouts" 
by JeremyVS Aug 22, 2013 

which was remixed from:

https://www.thingiverse.com/thing:92518
"Parametric Card Box"
by zxqueb May 24, 2013 
 
Version 5, March 2019
remixed by MC Geisler (mcgenki at gmail dot com)

customizable card box:
- you can specify wether you want cutouts in the top or bottom lid
- you can rotate the cutouts
- you can put a customizable text on the top and bottom lid
- top and bottom can be generated separately

Changed:
- the top and bottom box are more compact to print together
- the oval has a step at 0.85 mm, so you can change the filament there and get a colored text and oval
- Additional cutouts on the side to open the box a bit easier (you can press in the tabs which hold the shells together)

Have fun!

License: Attribution 4.0 International (CC BY 4.0)

You are free to:
    Share - copy and redistribute the material in any medium or format
    Adapt - remix, transform, and build upon the material
    for any purpose, even commercially.
*/



CARD_LONG_EDGE = CARD_SIZE_LONG_EDGE + (WALL_THICKNESS*2);
CARD_SHORT_EDGE = CARD_SIZE_SHORT_EDGE + (WALL_THICKNESS*2);
ALL_EXTRA_SPACE = (EXTRA_SPACE *2);

LID_OUTSIDE_LONG_THICKNESS = CARD_LONG_EDGE + (WALL_THICKNESS*2);
LID_OUTSIDE_SHORT_THICKNESS = CARD_SHORT_EDGE + (WALL_THICKNESS*2);

SPACE_BETWEEN_BOTTOM_AND_LID_Y = LID_OUTSIDE_SHORT_THICKNESS + (WALL_THICKNESS*2)+ 2;

HEIGHT = CARD_STACK_HEIGHT + WALL_THICKNESS;

// rounded cut-outs
// ends
END_CUTOUT_LENGTH = CARD_LONG_EDGE + 2;
END_CUTOUT_RADIUS = CARD_STACK_HEIGHT * END_CUTOUT_HEIGHT_PERCENTAGE/100;
END_CUTOUT_WIDTH  = CARD_SIZE_SHORT_EDGE * END_CUTOUT_WIDTH_PERCENTAGE/100;
// since we'll be scaling our cylinder, we really need the ratio to scale by for the longer dimension, not the direct dimension itself
END_CUTOUT_WIDTH_RATIO = END_CUTOUT_WIDTH / (END_CUTOUT_RADIUS*2);
// bottom
BOTTOM_CUTOUT_LENGTH = (WALL_THICKNESS * 2)+ 2;
BOTTOM_CUTOUT_RADIUS = CARD_SIZE_SHORT_EDGE * BOTTOM_CUTOUT_WIDTH_PERCENTAGE / 2 /100;
BOTTOM_CUTOUT_HEIGHT = CARD_SIZE_LONG_EDGE * BOTTOM_CUTOUT_LENGTH_PERCENTAGE/100;
// since we'll be scaling our cylinder, we really need the ratio to scale by for the longer dimension, not the direct dimension itself
BOTTOM_CUTOUT_HEIGHT_RATIO = BOTTOM_CUTOUT_HEIGHT / (BOTTOM_CUTOUT_RADIUS*2);

module writetext(textstr, sizeit)
{
        text(textstr,size=sizeit,font=fontname,halign="center",valign="center");
}

module cutout_ellipse()
{
    translate([0,0,-WALL_THICKNESS*0]) 
        rotate([0,0,Cutout_angle])
                scale([BOTTOM_CUTOUT_HEIGHT_RATIO,1,1]) 
                {    
                    translate([0,0,-1])
                        cylinder(h=WALL_THICKNESS+2,r=BOTTOM_CUTOUT_RADIUS);  //* cutout 
                    
                    difference()
                    {
                        translate([0,0,-1])
                            cylinder(h=WALL_THICKNESS/2+1,r=BOTTOM_CUTOUT_RADIUS+WALL_THICKNESS*1.2);//cutout outer - sunken in
                        translate([0,0,-1])
                            cylinder(h=WALL_THICKNESS+2,r=BOTTOM_CUTOUT_RADIUS+WALL_THICKNESS*.75);//cutout inner                          
                    }
                }
}

module writeit(text)
{
    rotate([0,0,-90])
        scale([-1,1,1])
            linear_extrude(height=WALL_THICKNESS/2, twist=0, slices=1, $fn=32, convexity = 32   )     
                writetext(text, textsize);
}


module bottom()
{    
     //*Start wedge 1
    translate ([((CARD_LONG_EDGE/2) -(TAB_THICKNESS/2)),-Wedge_thickness-EXTRA_SPACE,HEIGHT-WALL_THICKNESS-1-EXTRA_SPACE]) 

    difference ()
    {
        cube ([TAB_THICKNESS,Wedge_thickness+EXTRA_SPACE,WALL_THICKNESS+EXTRA_SPACE]);
        
        translate ([0, WALL_THICKNESS+(EXTRA_SPACE),0]) 
            rotate ([135,0,0])  
                cube ([(TAB_THICKNESS+3), sqrt(((WALL_THICKNESS+EXTRA_SPACE)*(WALL_THICKNESS+EXTRA_SPACE))+((WALL_THICKNESS+EXTRA_SPACE)*(WALL_THICKNESS+EXTRA_SPACE))), sqrt(((WALL_THICKNESS+EXTRA_SPACE)*(WALL_THICKNESS+EXTRA_SPACE))+((WALL_THICKNESS+EXTRA_SPACE)*(WALL_THICKNESS+EXTRA_SPACE)))]);

    }

    //*Start wedge 2
    translate ([((CARD_LONG_EDGE/2) - (TAB_THICKNESS/2)),CARD_SHORT_EDGE,HEIGHT-Wedge_thickness-1-EXTRA_SPACE])
        difference (){
            cube ([TAB_THICKNESS,Wedge_thickness+EXTRA_SPACE,WALL_THICKNESS+EXTRA_SPACE]);

            translate ([0 ,0 ,0]) rotate ([-45,0,0])  cube ([(TAB_THICKNESS+3),sqrt(((WALL_THICKNESS+EXTRA_SPACE)*(WALL_THICKNESS+EXTRA_SPACE))+((WALL_THICKNESS+EXTRA_SPACE)*(WALL_THICKNESS+EXTRA_SPACE))),sqrt(((WALL_THICKNESS+EXTRA_SPACE)*(WALL_THICKNESS+EXTRA_SPACE))+((WALL_THICKNESS+EXTRA_SPACE)*(WALL_THICKNESS+EXTRA_SPACE)))]);
        }

    translate ([((CARD_LONG_EDGE/2) -(TAB_THICKNESS/2)),-Wedge_thickness-EXTRA_SPACE,HEIGHT-1])  
        cube ([TAB_THICKNESS, Wedge_thickness+EXTRA_SPACE, 1]); 					//*Tab

    translate ([((CARD_LONG_EDGE/2) -(TAB_THICKNESS/2)),CARD_SHORT_EDGE,HEIGHT-1])  
        cube ([TAB_THICKNESS, Wedge_thickness+EXTRA_SPACE, 1]);				//*Tab

    difference()
    {
        translate ([-WALL_THICKNESS-EXTRA_SPACE, -WALL_THICKNESS-EXTRA_SPACE, 0 ]) 
            cube ([CARD_LONG_EDGE + (WALL_THICKNESS*2)+ALL_EXTRA_SPACE, CARD_SHORT_EDGE + (WALL_THICKNESS*2)+ALL_EXTRA_SPACE, WALL_THICKNESS]);  						//*Bottom Plate

        if (Cutout_hole_bottom == "true")
        {
            translate([CARD_LONG_EDGE/2,CARD_SHORT_EDGE/2,0]) 
                cutout_ellipse();
        }
        
        translate([textsize*1.2,(LID_OUTSIDE_SHORT_THICKNESS+ALL_EXTRA_SPACE)/2-WALL_THICKNESS-EXTRA_SPACE,-.1]) 
            writeit(Bottom_Text);
    }

    difference() 
    {
        cube ([CARD_LONG_EDGE,CARD_SHORT_EDGE,HEIGHT]);		//*Main Body

        translate([ ((CARD_LONG_EDGE / 2) + (TAB_THICKNESS/2)), -.5, ((HEIGHT+ WALL_THICKNESS) / 4) ]) 
            cube ([2, CARD_SHORT_EDGE + 2, HEIGHT]);	//*Tab cutout
        
        translate([ ((CARD_LONG_EDGE / 2) - ((TAB_THICKNESS/2)+2)), -.5, ((HEIGHT+ WALL_THICKNESS)/ 4) ]) 
            cube ([2, CARD_SHORT_EDGE + 2, HEIGHT ]);  //*Tab cutout

        translate([WALL_THICKNESS,WALL_THICKNESS,WALL_THICKNESS]) 
            cube ([CARD_LONG_EDGE - (WALL_THICKNESS*2),CARD_SHORT_EDGE - (WALL_THICKNESS*2),HEIGHT]);			//*Hole

        translate([-WALL_THICKNESS/2,CARD_SHORT_EDGE/2,HEIGHT]) scale([1,END_CUTOUT_WIDTH_RATIO,1])
            rotate([0,90,0]) 
                cylinder(h=END_CUTOUT_LENGTH,r=END_CUTOUT_RADIUS);  //*End cutout

        if (Cutout_hole_bottom == "true")
        {    
            translate([CARD_LONG_EDGE/2,CARD_SHORT_EDGE/2,0]) 
                cutout_ellipse(); //*Bottom cutout to cut through main box
        }
        
        translate([textsize*1.2,(LID_OUTSIDE_SHORT_THICKNESS+ALL_EXTRA_SPACE)/2-WALL_THICKNESS-EXTRA_SPACE,-.1]) 
            writeit(Bottom_Text);
    }
}


//------------------------
module top()
{
    difference() 
    {
        cube ([LID_OUTSIDE_LONG_THICKNESS+ALL_EXTRA_SPACE, LID_OUTSIDE_SHORT_THICKNESS+ALL_EXTRA_SPACE, HEIGHT]); 		//*Top Main Body


        translate([ ((LID_OUTSIDE_LONG_THICKNESS+ALL_EXTRA_SPACE) / 2)-((TAB_THICKNESS+1)/2), -.5, WALL_THICKNESS]) 
        {
            cube ([(TAB_THICKNESS+1), LID_OUTSIDE_SHORT_THICKNESS + 2, Wedge_thickness+1.15 ]); 	//*Tab Hole
            
            TOP_CUTOUT_LENGTH = LID_OUTSIDE_SHORT_THICKNESS + 2;          
            translate([ ((TAB_THICKNESS+1)/2), 
                        0, 
                        -WALL_THICKNESS]) 
            translate([0,-1,HEIGHT*1.3]) 
                scale([END_CUTOUT_WIDTH_RATIO,1,1])
                    rotate([0,90,90]) 
                        cylinder(h=TOP_CUTOUT_LENGTH+2,r=END_CUTOUT_RADIUS);  //*End cutout
        }
        
        translate([WALL_THICKNESS,WALL_THICKNESS, WALL_THICKNESS]) 
            cube ([LID_OUTSIDE_LONG_THICKNESS - (WALL_THICKNESS*2)+ALL_EXTRA_SPACE,LID_OUTSIDE_SHORT_THICKNESS - (WALL_THICKNESS*2)+ALL_EXTRA_SPACE,HEIGHT]);//*Hole
        
        if (Cutout_hole_top == "true")
        {
            translate([(LID_OUTSIDE_LONG_THICKNESS+ALL_EXTRA_SPACE)/2,(LID_OUTSIDE_SHORT_THICKNESS+ALL_EXTRA_SPACE)/2,0]) 
                cutout_ellipse();
        }
        
        translate([textsize*1.2,(LID_OUTSIDE_SHORT_THICKNESS+ALL_EXTRA_SPACE)/2,-.1]) 
            writeit(Top_Text);
    }
}
//----------------------------

if (type=="bottom" || type=="both")
    bottom();

if (type=="top" || type=="both")
    translate([0, ( type=="both" ? SPACE_BETWEEN_BOTTOM_AND_LID_Y : 0 ) ,0])
        top();

